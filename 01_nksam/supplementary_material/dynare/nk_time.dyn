% *************************************************************************
% Shopping Time and Frictional Goods Markets:
% Implications for the New-Keynesian Model
% Time-Allocation SaM Model
% -------------------------------------------------------------------------
% Konstantin Gantert
% Tilburg University
% Department of Economics
% k.gantert@tilburguniversity.edu
% 04/08/2025
% *************************************************************************
% -------------------------------------------------------------------------
% 1. Define Variables and Parameters
% -------------------------------------------------------------------------

var          // Aggregate quantities
                t, t_N          // Real final market goods (GDP)
                ivh, ivh_N      // Housework capital investment
                ivm, ivm_N      // Market capital investment
                kh, kh_N        // Housework capital stock
                km, km_N        // Market capital stock
                eh, eh_N        // Housework capital utilization
                em, em_N        // Market capital utilization
                hm, hm_N        // Total market work time
                hh, hh_N        // Total home production time
                hs, hs_N        // Total search time
                ue, ue_N        // Unemployment rate
                cm, cm_N        // Real market consumption
                ch, ch_N        // Housework production
                c, c_N          // Real consumption
                ym, ym_N        // Market production capacity
                sm, sm_N        // Market production available capacity
                et, et_N        // Short-run capacity utilization rate
                cu, cu_N        // Capacity utilization rate

             // Aggregate prices
                piC             // CPI inflation
                piW             // Nominal wage inflation
                piT             // Corrected price inflation
                w, w_N          // Real wage
                r, r_N          // Nominal bond interest rate
                rk, rk_N        // Market real capital interest rate
                mcY, mcY_N      // Real marginal costs capacity
                mcT, mcT_N      // Real marginal profits
                ps, ps_N        // Search goods price
                pt, pt_N        // Total goods price
                ped, ped_N      // Price elasticity of demand
                lw, lw_N        // Labor wedge

             // Search-and-matching variables
                x, x_N          // Goods market tightness
                f, f_N          // Goods finding rate
                q, q_N          // Goods selling rate

             // Adjustment costs
                chm, chm_N      // Hours adjustment costs
                chhm, chhm_N    // Marginal hours adjustment costs
                chi, chi_N      // Homework capital investment adjustment costs
                chhi, chhi_N    // Marginal homework capital investment adjustment costs
                cmi, cmi_N      // Market capital investment adjustment costs
                cmmi, cmmi_N    // Marginal market capital investment adjustment costs
                cp, cpp         // (Marginal) Nominal price adjustment costs
                cw, cww         // (Marginal) Nominal wage adjustment costs

             // Value functions
                muc, muc_N      // Marginal utility of consumption
                qm, qm_N        // Tobin'sm Q market capital stock
                qh, qh_N        // Tobin'sm Q homework capital stock
                vphi, vphi_N    // Real marginal pricing kernel
                
             // Marginal utility functions
                uC, uC_N        // Marginal aggregate consumption utility
                wCM, wCM_N      // Marginal market consumption utility
                wCH, wCH_N      // Marginal home consumption utility
                uHS, uHS_N      // Marginal search disutility
                uHH, uHH_N      // Marginal home production disutility
                uHM, uHM_N      // Marginal labor disutility

             // AR(1) shock processes
                sA              // Total factor productivity
                sZ              // Discount rate
                sM              // Monetary policy
                sP              // Cost-push
                sT              // Goods market mismatch
                sD              // Search effort

             // Gap variables
                gdp_gap         // Output gap
                cu_gap          // Capacity utilization gap
                ue_gap          // Unemployment gap

             // Observational variables
                gdp_obs         // Log real GDP
                h_obs           // Log total hours worked
                mc_obs          // Log real marginal cost
                w_obs           // Log real wage
                ls_obs          // Log labor share
                lpr_obs         // Log labor productivity
                lw_obs          // Log labor wedge
                hs_obs          // Log search effort
                ped_obs         // Log price elasticity of demand
                ;

varexo          eA              // Technology Shock
                eZ              // Discount Rate Shock
                eM              // Monetary Policy Shock
                eP              // Cost-Push Shock
                eT              // Goods market mismatch shock
                eD              // Search effort shock
                ;

parameters      betta sig iR iPi iGap
                epsW kapW muM nuM phiHM
                gamEH gamSH muH nuH
                gamES gamSS etaS psiS muS nuS delT delI epss kapP
                alpM alpH delM1 delM2 delM3 delH1 delH2 delH3 kapHI kapMI
                stdA stdZ stdM stdP stdD stdT
                rhoA rhoZ rhoM rhoP rhoT rhoD
                cuss uss hmss hshmss hhhmss ls_share markup nkpc_slope nkwpc_slope
                ghh markup_stst nkpc_target hours_target ls_target labor_ext hsAP
                ;

// Monetary Parameters
    set_param_value('betta', par.betta);
    set_param_value('sig', par.sig);
    set_param_value('iR', par.iR);
    set_param_value('iPi', par.iPi);
    set_param_value('iGap', par.iGap);
// Market Labor Parameters
    set_param_value('epsW', par.epsW);
    set_param_value('kapW', par.kapW);
    set_param_value('nuM', par.nuM);
    set_param_value('phiHM', par.phiHM);
// Homeproduction Labor Parameters
    set_param_value('gamEH', parHW.gamEH);
    set_param_value('gamSH', parHW.gamSH);
    set_param_value('nuH', parHW.nuH);
// Search Labor Parameters
    set_param_value('gamES', parSAM.gamES);
    set_param_value('gamSS', parSAM.gamSS);
    set_param_value('etaS', parSAM.etaS);
    set_param_value('nuS', parSAM.nuS);
    set_param_value('delT', parSAM.delT);
    set_param_value('delI', parSAM.delI);
    set_param_value('epss', par.epss);
    set_param_value('kapP', par.kapP);
// Capital Parameters
    set_param_value('alpM', par.alpM);
    set_param_value('alpH', parHW.alpH);
    set_param_value('delM1', par.delM1);
    set_param_value('delM2', par.delM2);
    set_param_value('delH1', par.delM1);
    set_param_value('delH2', par.delM2);
    set_param_value('kapMI', par.kapMI);
    set_param_value('kapHI', par.kapMI);
// Stochastic Processes
    set_param_value('stdA', shock.stdA);
    set_param_value('stdZ', shock.stdZ);
    set_param_value('stdM', shock.stdM);
    set_param_value('stdP', shock.stdP);
    set_param_value('stdT', shock.stdT);
    set_param_value('stdD', shock.stdD);
    set_param_value('rhoA', shock.rhoA);
    set_param_value('rhoZ', shock.rhoZ);
    set_param_value('rhoM', shock.rhoM);
    set_param_value('rhoP', shock.rhoP);
    set_param_value('rhoT', shock.rhoT);
    set_param_value('rhoD', shock.rhoD);
// Targets and switch parameters
    set_param_value('cuss', target.cuss);
    set_param_value('uss', target.uss);
    set_param_value('hmss', target.hmss);
    set_param_value('hshmss', target.hshmss);
    set_param_value('hhhmss', target.hhhmss);
    set_param_value('ls_share', target.ls_share);
    set_param_value('markup', target.markup);
    set_param_value('nkpc_slope', target.nkpc_slope);
    set_param_value('nkwpc_slope', target.nkwpc_slope);
// Functional form selection
    set_param_value('ghh', ghh_pref);
    set_param_value('markup_stst', markup_stst);
    set_param_value('hours_target', hours_target);
    set_param_value('ls_target', ls_target);
    set_param_value('nkpc_target', nkpc_target);
    set_param_value('labor_ext', labor_ext);
    set_param_value('hsAP', hsAP);

%--------------------------------------------------------------------------
% 2. MODEL BLOCK
%--------------------------------------------------------------------------
model;
%--------------------------------------------------------------------------
% 2.1 Goods Market Matching
%--------------------------------------------------------------------------

[name = 'Goods finding rate']
f       = psiS*sT * (gamES+(1-gamES)*x^(-gamSS))^(etaS/gamSS)   * hs^(etaS-1);
f_N     = psiS*sT * (gamES+(1-gamES)*x_N^(-gamSS))^(etaS/gamSS) * hs_N^(etaS-1);

[name = 'Goods selling rate']
q       = psiS*sT * (gamES*x^gamSS+(1-gamES))^(etaS/gamSS)   * sm^(etaS-1);
q_N     = psiS*sT * (gamES*x_N^gamSS+(1-gamES))^(etaS/gamSS) * sm_N^(etaS-1);

[name = 'Goods market tightness']
x       = hs   / sm;
x_N     = hs_N / sm_N;

[name = 'Customer relationship law of motion']
t       = (1-delT)*t(-1)   + q*sm;
t_N     = (1-delT)*t_N(-1) + q_N*sm_N;

%--------------------------------------------------------------------------
% 2.2 Constraints
%--------------------------------------------------------------------------

[name = 'Final goods use']
t       = cm   + ivm   + ivh;
t_N     = cm_N + ivm_N + ivh_N;

[name = 'Production function']
ym      = sA * hm^(1-alpM)   * (em*km(-1))^alpM;
ym_N    = sA * hm_N^(1-alpM) * (em_N*km_N(-1))^alpM;

[name = 'Homework production function']
ch      = hh^(1-alpH)   * (eh*kh(-1))^alpH;
ch_N    = hh_N^(1-alpH) * (eh_N*kh_N(-1))^alpH;

[name = 'Consumption composite good']
c       = ( gamEH*ch^gamSH   + (1-gamEH)*cm^gamSH )^(1/gamSH);
c_N     = ( gamEH*ch_N^gamSH + (1-gamEH)*cm_N^gamSH )^(1/gamSH);

[name = 'Beginning-of-period available final goods']
sm      =  ( ym   - (1-delT)*t(-1)   + (1-delI)*(1-q(-1))*sm(-1) ) * 1/(1+cp);
sm_N    =    ym_N - (1-delT)*t_N(-1) + (1-delI)*(1-q_N(-1))*sm_N(-1);

#delM   = delM1 + delM2*delM3/2*(em-1)^2   + delM3*(em-1);
#delM_N = delM1 + delM2*delM3/2*(em_N-1)^2 + delM3*(em_N-1);
[name = 'Market production capital stock']
km      = (1-delM)   * km(-1)   + ivm*(1-cmi);
km_N    = (1-delM_N) * km_N(-1) + ivm_N*(1-cmi_N);

#delH   = delH1 + delH2*delH3/2*(eh-1)^2   + delH3*(eh-1);
#delH_N = delH1 + delH2*delH3/2*(eh_N-1)^2 + delH3*(eh_N-1);
[name = 'Homework production capital stock']
kh      = (1-delH)   * kh(-1)   + ivh*(1-chi);
kh_N    = (1-delH_N) * kh_N(-1) + ivh_N*(1-chi_N);

[name = 'Short-run capacity utilization rate']
et      = t   / ym;
et_N    = t_N / ym_N;

[name = 'Unemployment rate']
ue      = ( w/muM * muc/(ghh*uC+(1-ghh)) )^(1/nuM) * hm^(-1) - 1;
ue_N    = ( w_N/muM * muc_N/(ghh*uC_N+(1-ghh)) )^(1/nuM) * hm_N^(-1) - 1;

%--------------------------------------------------------------------------
% 2.3 Households
%--------------------------------------------------------------------------
%--------------------------------------------------------------------------
% 2.3.1 Household Marginal Utility Functions

[name = 'Marginal utility of consumption']
uC      = ( c   - ghh*(  sD*muS*hs^(1+nuS)/(1+nuS) 
                       + muH*hh^(1+nuH)/(1+nuH) 
                       + muM*hm^(1+nuM)/(1+nuM)) )^(-sig);
uC_N    = ( c_N - ghh*(  sD*muS*hs_N^(1+nuS)/(1+nuS) 
                       + muH*hh_N^(1+nuH)/(1+nuH) 
                       + muM*hm_N^(1+nuM)/(1+nuM)) )^(-sig);

[name = 'Marginal disutility of home production']
uHH     = (-muH)*hh^nuH   * (ghh*uC+(1-ghh));
uHH_N   = (-muH)*hh_N^nuH * (ghh*uC_N+(1-ghh));

[name = 'Marginal disutility of market search']
uHS     = (-sD*muS)*hs^nuS   * (ghh*uC+(1-ghh));
uHS_N   = (-sD*muS)*hs_N^nuS * (ghh*uC_N+(1-ghh));

[name = 'Marginal disutility of market work']
uHM     = (-muM)*hm^nuM   * (ghh*uC+(1-ghh));
uHM_N   = (-muM)*hm_N^nuM * (ghh*uC_N+(1-ghh));

%--------------------------------------------------------------------------
% 2.3.2 Household FOCs

[name = 'Marginal net utility of consumption']
muc     = wCM   + uHS/f     - betta*sZ(+1)/sZ*(1-delT)*uHS(+1)/f(+1);
muc_N   = wCM_N + uHS_N/f_N - betta*sZ(+1)/sZ*(1-delT)*uHS_N(+1)/f_N(+1);

[name = 'Consumption Euler equation']
muc     = betta*sZ(+1)/sZ * (1+r)/(1+piC(+1)) * muc(+1);
muc_N   = betta*sZ(+1)/sZ * (1+r_N)           * muc_N(+1);

[name = 'Marginal utility of market goods']
wCM     = uC   * (1-gamEH) * (cm/c)^(gamSH-1);
wCM_N   = uC_N * (1-gamEH) * (cm_N/c_N)^(gamSH-1);

#hac     = 1 - chm   - chhm   + (1+piW(+1))/(1+r)*chhm(+1)*(hm(+1)/hm);
#hac_N   = 1 - chm_N - chhm_N + 1/(1+r)*chhm_N(+1)*(hm_N(+1)/hm_N);
[name = 'Market labor supply']
uHM     = (-1) * muc*w*(1-cw) * (epsW*hac^epsW-1)/(epsW*hac^epsW)
            - 1/(epsW*hac^epsW) * ( muc*w*cww - betta*sZ(+1)/sZ*muc(+1)*w(+1)*hm(+1)/hm*cww(+1) );
uHM_N   = (-1) * muc_N*w_N * (epsW*hac_N^epsW-1)/(epsW*hac_N^epsW);

[name = 'Tobins Q market capital stock']
qm      = betta*sZ(+1)/sZ * ( muc(+1)*rk(+1)*em(+1) + (1-delM)*qm(+1) );
qm_N    = betta*sZ(+1)/sZ * ( muc_N(+1)*rk_N(+1)*em_N(+1) + (1-delM_N)*qm_N(+1) );

[name = 'Market capital investment']
wCM     = qm*(1-cmi-cmmi)       + betta*sZ(+1)/sZ*(ivm(+1)/ivm)*cmmi(+1)*qm(+1);
wCM_N   = qm_N*(1-cmi_N-cmmi_N) + betta*sZ(+1)/sZ*(ivm_N(+1)/ivm_N)*cmmi_N(+1)*qm_N(+1);

[name = 'Market capital utilization']
rk      = qm/muc     * (delM2*delM3*(em-1)+delM3);
rk_N    = qm_N/muc_N * (delM2*delM3*(em_N-1)+delM3);

[name = 'Marginal utility of homeproduction goods']
wCH     = uC   * gamEH * c^(1-gamSH)   * ch^gamSH;
wCH_N   = uC_N * gamEH * c_N^(1-gamSH) * ch_N^gamSH;

[name = 'Homeproduction labor supply']
uHH     = (-1)*wCH*(1-alpH)/hh;
uHH_N   = (-1)*wCH_N*(1-alpH)/hh_N;

[name = 'Tobins Q homeproduction capital stock']
qh      = betta*sZ(+1)/sZ * ( wCH(+1)*alpH/kh     + (1-delH)*qh(+1) );
qh_N    = betta*sZ(+1)/sZ * ( wCH_N(+1)*alpH/kh_N + (1-delH_N)*qh_N(+1) );

[name = 'Homeproduction capital investment']
wCM     = qh*(1-chi-chhi)       + betta*sZ(+1)/sZ*(ivh(+1)/ivh)*chhi(+1)*qh(+1);
wCM_N   = qh_N*(1-chi_N-chhi_N) + betta*sZ(+1)/sZ*(ivh_N(+1)/ivh_N)*chhi_N(+1)*qh_N(+1);

[name = 'Homeproduction capital utilization']
wCH     = qh   * (delH2*delH3*(eh-1)+delH3)   * (eh*kh(-1))/alpH;
wCH_N   = qh_N * (delH2*delH3*(eh_N-1)+delH3) * (eh_N*kh_N(-1))/alpH;

%--------------------------------------------------------------------------
% 2.3 Final Good Firms
%--------------------------------------------------------------------------

[name = 'Labor demand']
w       = (1-alpM) * mcY   * ym/hm;
w_N     = (1-alpM) * mcY_N * ym_N/hm_N;

[name = 'Capital demand']
rk      = alpM * mcY   * ym/(em*km(-1));
rk_N    = alpM * mcY_N * ym_N/(em_N*km_N(-1));

[name = 'Pricing Constraint (Firms)']
vphi    = ( (-1)*mcT   + (1+piC(+1))/(1+r)*(1-delI)*mcY(+1) )
            * (etaS*gamES*x^gamSS)/((etaS-1)*gamES*x^gamSS-(1-gamES)-hsAP*nuS*(gamES*x^gamSS+(1-gamES))) * q*sm/ps;
vphi_N  = ( (-1)*mcT_N + 1/(1+r_N)*(1-delI)*mcY_N(+1) )
            * (etaS*gamES*x_N^gamSS)/((etaS-1)*gamES*x_N^gamSS-(1-gamES)-hsAP*nuS*(gamES*x_N^gamSS+(1-gamES)))*q_N*sm_N/ps_N;

[name = 'Profit Share (Firms)']
mcT      = 1 - vphi/t*wCM/(epss*sP*muc)         - (1+piC(+1))/(1+r)*(1-delT)*(mcY(+1)-mcT(+1));
mcT_N    = 1 - vphi_N/t_N*wCM_N/(epss*sP*muc_N) - 1/(1+r_N)*(1-delT)*(mcY_N(+1)-mcT_N(+1));

[name = 'Real Marginal Cost (Firms)']
mcY      = 1/(1+cp) * ( mcT*etaS*q     + vphi*(etaS-1-hsAP*nuS)*ps/sm           + (1+piC(+1))/(1+r)*(1-delI)*(1-etaS*q)*mcY(+1) );
mcY_N    =            ( mcT_N*etaS*q_N + vphi_N*(etaS-1-hsAP*nuS)*ps_N/sm_N + 1/(1+r_N)*(1-delI)*(1-etaS*q_N)*mcY_N(+1) );

[name = 'Price-Setting Equation (Firms)']
cpp     = t/(mcY*sm) * (1-vphi/t) + (1+piC(+1))/(1+r)*mcY(+1)/mcY*sm(+1)/sm*cpp(+1);
0       = 1 - vphi_N/t_N;

%--------------------------------------------------------------------------
% 2.4 Adjustment Costs
%--------------------------------------------------------------------------

[name = '(Marginal) market investment adjustment costs']
cmi     = kapMI/2*(ivm/ivm(-1)-1)^2;
cmi_N   = kapMI/2*(ivm_N/ivm_N(-1)-1)^2;
cmmi    = kapMI*(ivm/ivm(-1)-1)*ivm/ivm(-1);
cmmi_N  = kapMI*(ivm_N/ivm_N(-1)-1)*ivm_N/ivm_N(-1);

[name = '(Marginal) homeproduction investment adjustment costs']
chi     = kapHI/2*(ivh/ivh(-1)-1)^2;
chi_N   = kapHI/2*(ivh_N/ivh_N(-1)-1)^2;
chhi    = kapHI*(ivh/ivh(-1)-1)*ivh/ivh(-1);
chhi_N  = kapHI*(ivh_N/ivh_N(-1)-1)*ivh_N/ivh_N(-1);

[name = '(Marginal) Wage inflation adjustment costs']
cw      = kapW/2*piW^2;
cww     = kapW*piW*(1+piW);
piW     = w/w(-1)*(1+piC) - 1;

[name = '(Marginal) market hours adjustment costs']
chm     = phiHM/2*(hm/hm(-1)-1)^2;
chm_N   = phiHM/2*(hm_N/hm_N(-1)-1)^2;
chhm    = phiHM*(hm/hm(-1)-1)*hm/hm(-1);
chhm_N  = phiHM*(hm_N/hm_N(-1)-1)*hm_N/hm_N(-1);

[name = '(Marginal) Price inflation adjustment costs']
cp      = kapP/2*piC^2;
cpp     = kapP*piC*(1+piC);

%--------------------------------------------------------------------------
% 2.5 Prices and Elasticities
%--------------------------------------------------------------------------

[name = 'Total goods price']
pt      = wCM   / muc;
pt_N    = wCM_N / muc_N;

[name = 'Marginal goods finding cost']
ps      = -uHS   / (f*muc);
ps_N    = -uHS_N / (f_N*muc_N);

[name = 'Corrected purchase price inflation']
piT     = 1/(1+piC(+1))*muc(+1)/wCM(+1)*wCM/muc;

[name = 'Price elasticity of demand']
ped     = (-epss*sP) / ( 1 + ps - (1-delT)*(1+piC(+1))/(1+r)*ps(+1));
ped_N   = (-epss*sP) / ( 1 + ps_N - (1-delT)/(1+r_N)*ps_N(+1));

[name = 'Capacity utilization rate / efficiency wedge']
cu      = t   / (sA * hm^(1-alpM)   * km(-1)^alpM);
cu_N    = t_N / (sA * hm_N^(1-alpM) * km_N(-1)^alpM);

#vphiW      = (1-cw)*(epsW*hac^epsW-1)/(epsW*hac^epsW) 
                + (cww - (1+piW(+1))/(1+r)*hm(+1)/hm*cww(+1)) / (epsW*hac^epsW);
#vphiW_N    = (epsW*hac_N^epsW-1)/(epsW*hac_N^epsW);
[name = 'Labor wedge']
lw      = 1 - vphiW*mcY/et*em^alpM*muc/(ghh*uC+(1-ghh));
lw_N    = 1 - vphiW_N*mcY_N/et_N*em_N^alpM*muc_N/(ghh*uC_N+(1-ghh));

%--------------------------------------------------------------------------
% 2.5 Monetary Policy
%--------------------------------------------------------------------------

[name = 'Taylor rule']
((1+r)/(1+steady_state(r))) 
        = ((1+r(-1))/(1+steady_state(r)))^iR 
            * (((1+piC)/(1+steady_state(piC)))^iPi)^(1-iR)
            * ((1+gdp_gap)^iGap)^(1-iR)
            * sM;

%--------------------------------------------------------------------------
% 2.6 Stochastic processes
%--------------------------------------------------------------------------

[name = 'Technology shock AR(1) process']
log(sA) = rhoA*log(sA(-1)) + eA;

[name = 'Discount rate shock AR(1) process']
log(sZ) = rhoZ*log(sZ(-1)) + eZ;

[name = 'Monetary policy shock AR(1) process']
log(sM) = rhoM*log(sM(-1)) - eM;

[name = 'Cost-push shock AR(1) process']
log(sP) = rhoP*log(sP(-1)) + eP;

[name = 'Mismatch shock AR(1) process']
log(sT) = rhoT*log(sT(-1)) + eT;

[name = 'Search effort shock AR(1) process']
log(sD) = rhoD*log(sD(-1)) - eD;

%--------------------------------------------------------------------------
% 2.8 Gap and Observational Variables
%--------------------------------------------------------------------------

[name = 'Output Gap']
gdp_gap = t / t_N - 1;

[name = 'Capacity Utilization Gap']
cu_gap  = cu / cu_N - 1;

[name = 'Unemployment Gap']
ue_gap  = (1+ue) / (1+ue_N) - 1;

[name = 'Log Real GDP']
gdp_obs = log(t);

[name = 'Log Total Hours Worked']
h_obs   = log(hm);

[name = 'Log Real Wage']
w_obs   = log(w);

[name = 'Log Real Marginal Costs']
mc_obs  = log(mcY/et);

[name = 'Log Labor Share']
ls_obs  = log(w*hm/t);

[name = 'Log Labor Productivity']
lpr_obs = log(t/hm);

[name = 'Log Labor Wedge']
lw_obs  = log(lw);

[name = 'Log Search Hours']
hs_obs  = log(hs);

[name = 'Log Price Elasticity of Demand']
ped_obs = log(ped/steady_state(ped));

end;

%--------------------------------------------------------------------------
% 3. CHECK STEADY-STATES AND MODEL SETUP
%--------------------------------------------------------------------------

//check;
//resid;

%--------------------------------------------------------------------------
% 4. SIMULATION
%--------------------------------------------------------------------------

shocks;
    var eA      = stdA^2;
    var eZ      = stdZ^2;
    var eM      = stdM^2;
    var eP      = stdP^2;
    var eT      = stdT^2;
    var eD      = stdD^2;
end;

stoch_simul(nodisplay,
            nograph,
            noprint,
            pruning,
            order=@{approx_order},
            irf=@{irf_periods},
            periods=@{sim_periods},
            replic=@{replic_number},
            simul_replic=@{simul_replic_number},
            drop=@{drop_number}
            );

%----------------------------- End of File --------------------------------